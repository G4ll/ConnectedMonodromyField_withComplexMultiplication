/*  
    This script supports Example 9.1.
    
    We compute k(eJ)/k(EndJ) for J = Jac[y^2=x^15+1].
    The extension is generated by the algebraic periods
    P(sigma, f) as f varies in a family of generating
    equations for MT(J_15).
*/
AttachSpec("/home/gallo/ConnectedMonodromyField/CHIMP/CHIMP.spec");
SetLogFile("Example1.out");

/* ----------- setup ------------ */
Q := Rationals();
CC := ComplexField(1000);
P<x> := PolynomialRing(Q);

f := x^15+1;


/* -------- integration --------- */
/*  This is the integration bit:
    we compute the integrals \int_\sigma \omega_i
    for the eigenbasis \omega_i = x^{i-1} dx/y.     */
R := RealField(1000);
h := R!(2^-10);
N := Ceiling(7.2/h);
A_DE, W_DE := TanhSinhIntegrationPoints(N,h);

EndPoint0 := -Pi(R)/15;
EndPoint1 := Pi(R)/15;

A_DE := [ EndPoint0 * ( 1- 1/2*(a+1) ) + EndPoint1 * ( 1/2*(a+1) ) : a in A_DE ];    // linear transformation mapping [-1, 1] to the interval with endpoints EndPoint0 and EndPoint1
function f(j, z)
    return Exp(CC.1 * z * j)/(Sqrt(Exp(CC.1*15*z)+1));
end function;
elementary_period := [];
for j in [1..14] do
    elementary_integral := 1/2 * (EndPoint1-EndPoint0) * &+[ W_DE[k] * f(j, A_DE[k]) : k in [1..2*N+1] ]; // the funny-looking factor in front is supposed to come from the change of variables formula for integrals
    Append(~elementary_period, elementary_integral);
end for;


/* -------- period computation  --------- */
/*  We import the MT equations
    from Example 4.2.9 in our previous work
    "Monodromy groups and exceptional Hodge classes, I: Fermat Jacobians" */
MTequations := [
        [1, 14, 8, 7],
        [2, 13, 8, 7],
        [3, 12, 8, 7],
        [4, 11, 8, 7],
        [5, 10, 8, 7],
        [6,  9, 8, 7],
        [9, 12, 7, 2],
        [11, 12, 6, 1],
        [10,12,7,1]
    ];

/*  This bit omputes the periods associated
    to the MT equations,
    and returns the number field generates by those periods     */
KeA := CyclotomicField(15);
for If in MTequations do 
    period := &*[ elementary_period[i] : i in If ] / (2 * Pi(CC) * CC.1)^2;
    mu := MinimalPolynomialExtra(period, RationalsExtra(1000));
    if #Roots(mu, KeA) eq 0 then
        "The equation", f, "gives a period not in Q(zeta_15)";
        KeA := Compositum(KeA, NumberField(mu));
    end if;
end for;

"The connected monodromy field is the", KeA;

/*  The number field K(eA) we computed in the previous work */
Kconn := NumberField(x^16 - 5*x^15 + 14*x^14 - 30*x^13 + 57*x^12 - 100*x^11 + 157*x^10 - 215*x^9 + 250*x^8 - 240*x^7 + 183*x^6 - 110*x^5 + 57*x^4 - 30*x^3 + 16*x^2 - 5*x + 1);

/* Check that the results agree: */
"And it is isomorphic to the predicted one:";
IsIsomorphic(Kconn, KeA);